#+ summary: Bio2RDF What drugs/compounds target gene products of [gene]?
#+ endpoint: http://bio2rdf.org/sparql
#+ tags:
#+   - Bio2RDF
#+ method: GET
#+ pagination: 50

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX db: <http://bio2rdf.org/drugbank_vocabulary:>
PREFIX hgnc: <http://bio2rdf.org/hgnc_vocabulary:>
PREFIX omim: <http://bio2rdf.org/omim_vocabulary:>
SELECT distinct ?omimGene ?geneName ?drug ?drugLabel
{
 ?drug a db:Drug .
 ?drug db:target ?target .
 ?drug rdfs:label ?drugLabel .
 ?target a db:Target .
 ?target db:gene-name ?geneName .
 FILTER regex(str(?geneName), "SLC7A3") .
 OPTIONAL {
  ?target db:x-hgnc ?hgncGeneSymbol .
  ?hgncGeneSymbol hgnc:x-omim ?omimGene .
  ?omimGene a omim:Gene .
 }
}